buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'java'

sourceSets {
    main {
        java {
            srcDir "src"
            exclude '**/test/**'
        }
    }
    test {
        java { srcDir "src/test" }
    }
}

compileJava.doFirst {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

test {
    // To fix issue with tests
    scanForTestClasses = false

    maxParallelForks = 3
    testLogging.showStandardStreams = true
    testLogging { events 'passed' }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

group = 'com.qiwi.utils'
version = '1.0'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['jar', 'sources']
    publish = true
    pkg {
        repo = 'maven'
        name = project.group + '.java-xmlbuilder-light'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/qiwi/java-xmlbuilder-light.git'
        version {
            name = project.version
            desc = 'Android publishing plugin'
            released  = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': project.group + ':java-xmlbuilder-light:' + project.version]
        }
    }
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'java-xmlbuilder-light'
            version project.version
        }
        sources(MavenPublication) {
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}